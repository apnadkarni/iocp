# Core iocp package
package ifneeded @PACKAGE_NAME@ @PACKAGE_VERSION@ \
    [list apply [list {dir} {
        package require platform
        set initName [string totitle @PACKAGE_NAME@]
        if {[package vsatisfies [package require Tcl] 9]} {
            set fileName "@PKG_LIB_FILE9@"
        } else {
            set fileName "@PKG_LIB_FILE8@"
        }
        set platformId [platform::identify]
        set searchPaths [list [file join $dir $platformId] \
                             {*}[lmap platformId [platform::patterns $platformId] {
                                 file join $dir $platformId
                             }] \
                             $dir]
        foreach path $searchPaths {
            set lib [file join $path $fileName]
            if {[file exists $lib]} {
                uplevel #0 [list load $lib $initName]
                return
            }
        }
        error "Could not locate $fileName in directories [join $searchPaths {, }]"
    }] $dir]

# iocp_inet doesn't need anything other than core iocp
package ifneeded @PACKAGE_NAME@_inet @PACKAGE_VERSION@ \
    "package require @PACKAGE_NAME@"

if {@ENABLE_BLUETOOTH@} {
    # iocp_bt needs supporting script files
    package ifneeded @PACKAGE_NAME@_bt @PACKAGE_VERSION@ \
        "[list source [file join $dir bt.tcl]]"
}

